# Feature tests

support =
  element: do ->
    element = document.createElement("details")
    return false unless "open" of element

    element.innerHTML = "<summary>a</summary>b"
    element.setAttribute("style", "position: absolute; left: -9999px")

    parent = document.body ? document.documentElement
    parent.appendChild(element)
    closedHeight = element.offsetHeight
    element.open = true
    openedHeight = element.offsetHeight
    parent.removeChild(element)

    closedHeight isnt openedHeight

  toggleEvent: do ->
    element = document.createElement("details")
    "ontoggle" of element

return if support.element and support.toggleEvent

# Polyfill installers

polyfillStyles = ->
  document.head.insertAdjacentHTML("afterbegin", """<style><%= depend_on_asset("./polyfill.css").to_s.gsub(/\s/, "").strip %></style>""")

polyfillProperties = ->
  {prototype} = document.createElement("details").constructor
  {setAttribute, removeAttribute} = prototype
  setOpen = Object.getOwnPropertyDescriptor(prototype, "open")?.set

  Object.defineProperties prototype,
    open:
      set: (value) ->
        if @tagName is "DETAILS"
          if value
            @setAttribute("open", "")
          else
            @removeAttribute("open")
          value
        else
          setOpen?.call(this, value)

    setAttribute:
      value: (name, value) ->
        triggerToggleIfToggled this, =>
          setAttribute.call(this, name, value)

    removeAttribute:
      value: (name) ->
        triggerToggleIfToggled this, =>
          removeAttribute.call(this, name)

polyfillToggle = ->
  onTogglingTrigger (element) ->
    summary = element.querySelector "summary"
    if element.hasAttribute("open")
      element.removeAttribute("open")
      summary.setAttribute("aria-expanded", false)
    else
      element.setAttribute("open", "")
      summary.setAttribute("aria-expanded", true)

polyfillFocusAndARIA = ->
  summaries = document.querySelectorAll("summary")
  for summary in summaries
    addAttributesForSummary(summary)
  if MutationObserver?
    observer = new MutationObserver (mutations) ->
      for {addedNodes} in mutations
        for target in addedNodes
          if target.tagName is "DETAILS"
            summary = target.querySelector "summary"
            addAttributesForSummary(summary) if summary
    observer.observe(document.documentElement, attributes: true, subtree: true, childList: true)
  else
    document.addEventListener "DOMNodeInserted", (event) ->
      if event.target.tagName is "SUMMARY"
        addAttributesForSummary event.target

addAttributesForSummary = (summary) ->
  summary.setAttribute "tabindex", "0"
  summary.setAttribute "aria-expanded", "false"
  summary.setAttribute "role", "button"

polyfillToggleEvent = ->
  if MutationObserver?
    observer = new MutationObserver (mutations) ->
      for {target, attributeName} in mutations
        if target.tagName is "DETAILS" and attributeName is "open"
          triggerToggle(target)

    observer.observe(document.documentElement, attributes: true, subtree: true)
  else
    onTogglingTrigger (element) ->
      open = element.getAttribute("open")
      setTimeout ->
        unless element.getAttribute("open") is open
          triggerToggle(element)
      , 1

# Event helpers

clickEventIsSignificant = (event) ->
  not (
    event.defaultPrevented or
    event.which > 1 or
    event.altKey or
    event.ctrlKey or
    event.metaKey or
    event.shiftKey or
    event.target.isContentEditable
  )

keydownEventIsSignificant = (event) ->
  not (
    event.defaultPrevented or
    (event.keyCode != 32 and event.keyCode != 13) or
    event.altKey or
    event.ctrlKey or
    event.metaKey or
    event.shiftKey or
    event.target.isContentEditable
  )

onTogglingTrigger = (callback) ->
  addEventListener "click", (event) ->
    if clickEventIsSignificant(event)
      if element = findClosestElementWithTagName(event.target, "SUMMARY")
        if element.parentElement?.tagName is "DETAILS"
          callback(element.parentElement)
  , false

  addEventListener "keydown", (event) ->
    if keydownEventIsSignificant(event)
      if element = findClosestElementWithTagName(event.target, "SUMMARY")
        if element.parentElement?.tagName is "DETAILS"
          callback(element.parentElement)
          # Prevent pagedown from space key
          event.preventDefault()
  , false

findClosestElementWithTagName = do ->
  if typeof Element::closest is "function"
    (element, tagName) ->
      element.closest(tagName)
  else
    (element, tagName) ->
      while element
        if element.tagName is tagName
          return element
        else
          element = element.parentElement

triggerToggle = (element) ->
  event = document.createEvent("Events")
  event.initEvent("toggle", true, false)
  element.dispatchEvent(event)

triggerToggleIfToggled = (element, fn) ->
  open = element.getAttribute("open")
  result = fn()
  triggerToggle(element) unless element.getAttribute("open") is open
  result

# Install polyfills

unless support.element
  polyfillStyles()
  polyfillProperties()
  polyfillToggle()
  polyfillFocusAndARIA()

if support.element and not support.toggleEvent
  polyfillToggleEvent()
